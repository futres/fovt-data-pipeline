prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>
prefix urn: <urn:>

# Use the concat/group_concat function to push all plantStructurePresence types into an array within a field.
# this enables ElasticSearch to index this easily while shrinking output file sizes
SELECT ?observationID ?elementID ?beginDateCertainty ?endDateCertainty ?methodOfDateDerivation ?yearMeasured ?monthMeasured ?dayMeasured ?decimalLatitude ?decimalLongitude ?imageURI ?minimumChronometricAge ?maximumChronometricAge ?associatedSequences ?scientificName ?taxonRank ?organismID ?anatomicalFeature ?measurementValue ?measurementUnit ?measurementMethod (group_concat(distinct ?traitTypes;separator='|') as ?traits)

WHERE {    

	# Create a list of traits for organisms -- these will be grouped by in the "GROUP BY" clause
        ?vertebrateScalarTrait rdf:type ?traitTypes .

	# Do not report namedindividual, not particularly useful in our output
        FILTER (?traitTypes != <http://www.w3.org/2002/07/owl#NamedIndividual>) .

	# The joins here are largely derived from the relations.csv file
	# since these can be different for different projects we need to think carefully
	# how to address joins in this section for the different projects.
	# Using SPARQL property paths is one possible solution
	?vertebrateOrganism rdf:type <http://purl.obolibrary.org/obo/NCBITaxon_7742>.
	?scalarMeasurementDatum obo:OVT_0010000 ?vertebrateScalarTrait .
	# has quality measurement (following) is the inverse of quality measurement of (above)
	#?vertebrateScalarTrait obo:OVT_0010002 ?scalarMeasurementDatum .
	?vertebrateScalarTrait obo:RO_0000080 ?vertebrateOrganism .
	?organismalTraitObsProcess obo:OBI_0000299 ?scalarMeasurementDatum .
	?vertebrateOrganism obo:OBI_0000295 ?organismalTraitObsProcess .

	# Observing Process Attributes
	OPTIONAL {?organismalTraitObsProcess dwc:eventID ?observationID} .
	OPTIONAL {?organismalTraitObsProcess dwc:measurementID ?elementID} .
	OPTIONAL {?organismalTraitObsProcess urn:beginDateCertainty ?beginDateCertainty} .
	OPTIONAL {?organismalTraitObsProcess urn:endDateCertainty ?endDateCertainty} .
	OPTIONAL {?organismalTraitObsProcess urn:methodOfDateDerivation ?methodOfDateDerivation} .
	OPTIONAL {?organismalTraitObsProcess dwc:year ?yearMeasured} .
	OPTIONAL {?organismalTraitObsProcess dwc:month ?monthMeasured} .
	OPTIONAL {?organismalTraitObsProcess dwc:day ?dayMeasured} .
	OPTIONAL {?organismalTraitObsProcess dwc:decimalLatitude ?decimalLatitude} .
	OPTIONAL {?organismalTraitObsProcess dwc:decimalLongitude ?decimalLongitude} .
	OPTIONAL {?organismalTraitObsProcess dwc:associatedMedia ?imageURI} .
	OPTIONAL {?organismalTraitObsProcess urn:minimumChronometricAge ?minimumChronometricAge} .
	OPTIONAL {?organismalTraitObsProcess urn:maximumChronometricAge ?maximumChronometricAge} .
	OPTIONAL {?organismalTraitObsProcess dwc:associatedSequences ?associatedSequences} .  

	# Organism Attributes
        OPTIONAL {?vertebrateOrganism dwc:scientificName ?scientificName} . 
        OPTIONAL {?vertebrateOrganism dwc:taxonRank ?taxonRank} . 
	OPTIONAL {?vertebrateOrganism dwc:organismID ?organismID} .

	# Trait Attributes
	OPTIONAL {?vertebrateScalarTrait urn:anatomicalFeature ?anatomicalFeature} .

	# Scalar Measurement Datum
	OPTIONAL {?scalarMeasurementDatum dwc:measurementValue ?measurementValue} .
	OPTIONAL {?scalarMeasurementDatum dwc:measurementUnit ?measurementUnit} .
	OPTIONAL {?scalarMeasurementDatum dwc:measurementMethod ?measurementMethod} .

}

GROUP BY ?observationID ?elementID ?beginDateCertainty ?endDateCertainty ?methodOfDateDerivation ?yearMeasured ?monthMeasured ?dayMeasured ?decimalLatitude ?decimalLongitude ?imageURI ?minimumChronometricAge ?maximumChronometricAge ?associatedSequences ?scientificName ?taxonRank ?organismID ?anatomicalFeature ?measurementValue ?measurementUnit ?measurementMethod 
ORDER BY ASC(?observationID)

